### Realizar un programa que lea y almacene la información de 400 alumnos ingresantes a la Facultad de Informática de la UNLP en el año 2020. De cada alumno se lee: nro de inscripción, DNI, apellido, nombre y año de nacimiento. Una vez leída y almacenada toda la información, calcular e informar:
### a) El porcentaje de alumnos con DNI compuesto sólo por dígitos pares.
### b) Apellido y nombre de los dos alumnos de mayor edad.
##
```pascal
program InfoUNLP;
const
  ingresantes = 400;
type
  alumno = record
    nroInscripcion: integer;
    dni: int64;
    apellido: string;
    nombre: string;
    anoNacimiento: integer;
  end;

  tAlumnos = array[1..ingresantes] of alumno;

  tMayorEdad = array[1..2] of alumno;

  tStats = record
    porcentajePares: real;
    mayorEdad: tMayorEdad;
  end;

function soloPares(numero:int64): boolean;
var 
  esPar: boolean;
begin
  esPar := true;
  while ((numero > 0) and (esPar=true)) do
    begin
      esPar := (numero mod 2 = 0);
      numero := numero div 10;
    end;
  soloPares := esPar;
end;

function randomInt(min, max: int64) : int64;
begin
  if (min < 0) then
    min := min * -1;

  randomInt := Random(max - min) + min;
end;

procedure GenerarNombreApellidoAleatorio(var Nombre, Apellido: string);
const
  SilabasNombres: array[1..20] of string = ('ana', 'mar', 'jua', 'ped', 'pau', 'car', 'lu', 'ma', 'fe', 'so',
                                            'li', 'ro', 'me', 'pa', 'no', 'la', 'sa', 'lu', 'ba', 'mi');
  SilabasApellidos: array[1..20] of string = ('gon', 'mor', 'rom', 'gue', 'fer', 'ram', 'gar', 'ber', 'per', 'del',
                                              'tor', 'her', 'dia', 'vaz', 'cal', 'sal', 'ver', 'guez', 'ros', 'mart');
var
  LongitudNombre, LongitudApellido, i: integer;
begin
  LongitudNombre := Random(3) + 2;
  LongitudApellido := Random(2) + 2;
  Nombre := ''; Apellido := '';
  
  for i := 1 to LongitudNombre do
    Nombre := Nombre + SilabasNombres[Random(20) + 1];

  for i := 1 to LongitudApellido do
    Apellido := Apellido + SilabasApellidos[Random(20) + 1];

  Nombre[1] := UpCase(Nombre[1]);
  Apellido[1] := UpCase(Apellido[1]);
end;

procedure cargarAlumnosConRandom(var alumnos: tAlumnos);
var
  i: integer;
begin
  for i := 1 to ingresantes do
  begin;
    writeln;
    alumnos[i].nroInscripcion := randomInt(10000, 12000);
    writeln('Ingrese el Número de Inscripción: ', alumnos[i].nroInscripcion);
  
    alumnos[i].dni := randomInt(30000000, 42000000);
    writeln('Ingrese el DNI: ', alumnos[i].dni);
  
    GenerarNombreApellidoAleatorio(alumnos[i].nombre, alumnos[i].apellido);
    writeln('Ingrese el Apellido: ', alumnos[i].apellido);
    writeln('Ingrese el Nombre: ', alumnos[i].nombre);
  
    alumnos[i].anoNacimiento := randomInt(1970, 2005);
    writeln('Ingrese el Año de Nacimiento: ', alumnos[i].anoNacimiento);
  end;
end;

procedure procesar(alumnos: tAlumnos; var stats: tStats);
var
  i, cPares: integer;
  mEdad : array[1..2] of integer;
begin
  cPares := 0;
  mEdad[1] := 3000;
  mEdad[2] := 3000;

  for i := 1 to ingresantes do
  begin
    if (soloPares(alumnos[i].dni)) then
      cPares := cPares + 1;

    if (alumnos[i].anoNacimiento < mEdad[1]) then
    begin
      mEdad[2] := mEdad[1];
      mEdad[1] := alumnos[i].anoNacimiento;
      stats.mayorEdad[2] := stats.mayorEdad[1];
      stats.mayorEdad[1] := alumnos[i];
    end
    else
      if (alumnos[i].anoNacimiento < mEdad[2]) then
      begin
        mEdad[2] := alumnos[i].anoNacimiento;
        stats.mayorEdad[2] := alumnos[i];
      end;
  end;

  stats.porcentajePares := cPares / ingresantes * 100;
end;

procedure informar(stats: tStats);
begin
  writeln;
  writeln('Porcentaje de DNIs compuestos sólo por dígitos pares: ', stats.porcentajePares:0:2, '%');

  writeln;
  writeln('Alumnos de mayor edad:');
  write(' - ', stats.mayorEdad[1].apellido, ' ', stats.mayorEdad[1].nombre);
  writeln('(',stats.mayorEdad[1].anoNacimiento,')');

  write(' - ', stats.mayorEdad[2].apellido, ' ', stats.mayorEdad[2].nombre);
  writeln('(',stats.mayorEdad[2].anoNacimiento,')');
end;

var
  alumnos : tAlumnos;
  stats : tStats;
begin
  Randomize;
  cargarAlumnosConRandom(alumnos);
  procesar(alumnos, stats);
  informar(stats);
end.
```